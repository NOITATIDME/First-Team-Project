create or replace PACKAGE BODY PKG_HBOARD AS

  -- 게시글 추가 / 답글 추가
  PROCEDURE PROC_BOARD_INSERT( IN_MENU_ID IN VARCHAR2, IN_TITLE IN VARCHAR2, IN_CONT IN VARCHAR2, IN_WRITER  IN VARCHAR2, IN_BNUM IN NUMBER, IN_LVL IN NUMBER, IN_STEP IN NUMBER, IN_NREF IN NUMBER) AS
    V_IDX  NUMBER(10);
    V_BNUM NUMBER(10);
    V_LVL  NUMBER(10);
    V_STEP NUMBER(10);
    V_NREF NUMBER(10);
  BEGIN
    -- IDX : 전체 게시글의 번호
    SELECT NVL(MAX(IDX), 0) + 1
        INTO V_IDX
    FROM HBOARD;
    
    IF IN_BNUM = 0 THEN  -- 새글
      -- BNUM : 해당 MENU 의 글번호
      SELECT NVL(MAX(BNUM), 0) + 1
      INTO V_BNUM
      FROM HBOARD
      WHERE MENU_ID = IN_MENU_ID;
    
    V_LVL  := 0;  -- 답변글의 깊이
    V_STEP := 0;  -- 답변글 출력순서
    
    -- 글그룹번호 == BNUM
    SELECT NVL(MAX(NREF), 0) + 1
    INTO V_NREF
    FROM HBOARD
    WHERE MENU_ID = IN_MENU_ID;
    
    ELSE  -- 답글 (IN_BNUM !=0)
      V_BNUM  := IN_BNUM;
      V_LVL   := IN_LVL + 1;
      V_STEP  := IN_STEP + 1;
      
      UPDATE HBOARD
      SET    STEP = STEP + 1
      WHERE  MENU_ID = IN_MENU_ID
      AND    NREF    = IN_NREF
      AND    STEP >= V_STEP;
      
      V_NREF  := IN_NREF;
      
      END IF;
      
    INSERT INTO HBOARD(
        IDX,
        TITLE,
        CONT,
        WRITER,
        REGDATE,
        READCOUNT,
        BNUM,
        LVL,
        STEP,
        NREF,
        MENU_ID,
        DELNUM
    )
    VALUES(
        V_IDX,
        IN_TITLE,
        IN_CONT,
        IN_WRITER,
        SYSDATE,
        0,
        V_BNUM,
        V_LVL,
        V_STEP,
        V_NREF,
        IN_MENU_ID,
        0
    );
    COMMIT;
    
  END PROC_BOARD_INSERT;

  -- 게시글 페이징 목록 조회
  PROCEDURE PROC_BOARD_PAGINGLIST( IN_MENU_ID IN VARCHAR2, IN_NOWPAGE IN NUMBER, IN_PAGECOUNT IN NUMBER, OUT_TOTALCOUNT OUT NUMBER, O_CUR OUT SYS_REFCURSOR) AS
    V_STARTNUM NUMBER(8);
    V_ENDNUM NUMBER(8);
  BEGIN
  
    -- 페이징을 위한 전체자료수
    SELECT    COUNT(IDX)
    INTO     OUT_TOTALCOUNT
    FROM     HBOARD
    WHERE    MENU_ID  =  IN_MENU_ID; 
    
    -- 가져올 자료의 시작번호, 끝번호
    V_STARTNUM  :=  (IN_NOWPAGE-1) * IN_PAGECOUNT + 1;
    V_ENDNUM    :=  IN_NOWPAGE * IN_PAGECOUNT; 
     
    IF( V_ENDNUM >= OUT_TOTALCOUNT  )  THEN
      V_ENDNUM := OUT_TOTALCOUNT; 
    END IF;
    
    -- DATA 가져오기
    OPEN O_CUR FOR
      SELECT  * FROM
        (
          SELECT
            ROW_NUMBER() OVER( ORDER BY  NREF DESC, STEP ASC ) AS RN,
            IDX, MENU_ID,
            TITLE, WRITER, 
            TO_CHAR(REGDATE,'YYYY-MM-DD HH24:MI:SS') REGDATE, 
            C.CNAME,
            READCOUNT,
            BNUM, LVL, STEP, NREF, DELNUM
          FROM
            HBOARD H JOIN CLIENTINFO C
          ON
            H.WRITER = C.CID
          AND  
            MENU_ID    =    IN_MENU_ID           
       ) T
       WHERE T.RN   BETWEEN   V_STARTNUM  AND   V_ENDNUM;
    
  END PROC_BOARD_PAGINGLIST;

  -- 게시글 조회
  PROCEDURE PROC_BOARD_VIEW( IN_IDX IN NUMBER, O_CUR  OUT SYS_REFCURSOR) AS
  BEGIN
    -- 조회수 증가
    UPDATE HBOARD
    SET READCOUNT = READCOUNT + 1
    WHERE IDX     = IN_IDX;
    COMMIT;
    
    -- IDX 로 조회
    
  
    OPEN O_CUR FOR
    SELECT 
        IDX,
        MENU_ID,
        TITLE,
        CONT,
        WRITER,
        TO_CHAR(REGDATE, 'YYYY-MM-DD HH24:MI:SS') REGDATE,
        READCOUNT,
        BNUM,
        LVL,
        STEP,
        NREF,
        DELNUM
    FROM HBOARD
    WHERE IDX = IN_IDX;
  END PROC_BOARD_VIEW;

  -- 게시글 수정
  PROCEDURE PROC_BOARD_UPDATE( IN_IDX IN NUMBER, IN_TITLE IN VARCHAR2, IN_CONT IN VARCHAR2) AS
  BEGIN
    UPDATE HBOARD
    SET TITLE = IN_TITLE, CONT = IN_CONT
    WHERE IDX = IN_IDX;
  END PROC_BOARD_UPDATE;

  -- 게시글 댓글 추가
  PROCEDURE PROC_BOARD_COMMENTINSERT(
    IN_COMMENT_HBOARD IN NUMBER,
    IN_COMMENT_WRITER IN VARCHAR2,
    IN_COMMENT_CONT IN VARCHAR2,
    IN_COMMENT_BNUM IN NUMBER,
    IN_COMMENT_LVL IN NUMBER,
    IN_COMMENT_STEP IN NUMBER,
    IN_COMMENT_NREF IN NUMBER
  ) AS
   V_COMMENT_IDX  NUMBER(5,0);
   V_COMMENT_BNUM NUMBER(10);
   V_COMMENT_LVL  NUMBER(10);
   V_COMMENT_STEP NUMBER(10);
   V_COMMENT_NREF NUMBER(10);
  BEGIN
    -- IDX : 전체 댓글의 번호
    SELECT NVL(MAX(COMMENT_IDX), 0) + 1
        INTO V_COMMENT_IDX
    FROM HBOARD_COMMENT;
      
    IF IN_COMMENT_BNUM = 0 THEN  -- 새글
      -- COMMENT_BNUM : 해당 댓글 의 글번호
      SELECT NVL(MAX(COMMENT_BNUM), 0) + 1
      INTO V_COMMENT_BNUM
      FROM HBOARD_COMMENT
      WHERE COMMENT_HBOARD = IN_COMMENT_HBOARD;
    
    V_COMMENT_LVL  := 0;  -- 답변글의 깊이
    V_COMMENT_STEP := 0;  -- 답변글 출력순서
    
    -- 글그룹번호 == BNUM
    SELECT NVL(MAX(COMMENT_NREF), 0) + 1
    INTO V_COMMENT_NREF
    FROM HBOARD_COMMENT
    WHERE COMMENT_HBOARD = IN_COMMENT_HBOARD;
    
    ELSE  -- 답글 (IN_BNUM !=0)
      V_COMMENT_BNUM  := IN_COMMENT_BNUM;
      V_COMMENT_LVL   := IN_COMMENT_LVL + 1;
      V_COMMENT_STEP  := IN_COMMENT_STEP + 1;
      
      UPDATE HBOARD_COMMENT
      SET    COMMENT_STEP = COMMENT_STEP + 1
      WHERE  COMMENT_HBOARD = IN_COMMENT_HBOARD
      AND    COMMENT_NREF    = IN_COMMENT_NREF
      AND    COMMENT_STEP >= V_COMMENT_STEP;
      
      V_COMMENT_NREF  := IN_COMMENT_NREF;
      
      END IF;
        
    INSERT INTO HBOARD_COMMENT(
        COMMENT_IDX,
        COMMENT_HBOARD,
        COMMENT_WRITER,
        COMMENT_REGDATE,
        COMMENT_CONT,
        COMMENT_BNUM,
        COMMENT_LVL,
        COMMENT_STEP,
        COMMENT_NREF,
        COMMENT_DELNUM
    )
    VALUES(
        V_COMMENT_IDX,
        IN_COMMENT_HBOARD,
        IN_COMMENT_WRITER,
        SYSDATE,
        IN_COMMENT_CONT,
        V_COMMENT_BNUM,
        V_COMMENT_LVL,
        V_COMMENT_STEP,
        V_COMMENT_NREF,
        0
    );
    COMMIT;
    
  END PROC_BOARD_COMMENTINSERT;

  -- 게시글 삭제
  PROCEDURE PROC_BOARD_DELETE( IN_IDX IN NUMBER ) AS
  BEGIN
    DELETE FROM HBOARD_COMMENT
    WHERE COMMENT_HBOARD = IN_IDX;
  
    DELETE FROM HBOARD
    WHERE IDX = IN_IDX;
  END PROC_BOARD_DELETE;

  -- 게시글 댓글 페이징 조회
  PROCEDURE PROC_BOARD_COMMENTPAGINGLIST( IN_IDX IN VARCHAR2, IN_NOWPAGE IN NUMBER, IN_PAGECOUNT IN NUMBER, OUT_TOTALCOUNT OUT NUMBER, O_CUR OUT SYS_REFCURSOR) AS
    V_STARTNUM NUMBER(8);
    V_ENDNUM NUMBER(8);
  BEGIN
-- 페이징을 위한 전체자료수
    SELECT    COUNT(COMMENT_IDX)
    INTO     OUT_TOTALCOUNT
    FROM     HBOARD_COMMENT
    WHERE    COMMENT_HBOARD  =  IN_IDX; 
    
    -- 가져올 자료의 시작번호, 끝번호
    V_STARTNUM  :=  (IN_NOWPAGE-1) * IN_PAGECOUNT + 1;
    V_ENDNUM    :=  IN_NOWPAGE * IN_PAGECOUNT; 
     
    IF( V_ENDNUM >= OUT_TOTALCOUNT  )  THEN
      V_ENDNUM := OUT_TOTALCOUNT; 
    END IF;
    
    -- DATA 가져오기
    OPEN O_CUR FOR
      SELECT  * FROM
        (
          SELECT
            ROW_NUMBER() OVER( ORDER BY  COMMENT_NREF DESC, COMMENT_STEP ASC ) AS RN,
            COMMENT_IDX, COMMENT_HBOARD,
            COMMENT_WRITER, TO_CHAR(COMMENT_REGDATE,'YYYY-MM-DD HH24:MI:SS') COMMENT_REGDATE, 
            COMMENT_CONT, COMMENT_BNUM, COMMENT_LVL, COMMENT_STEP, COMMENT_NREF, COMMENT_DELNUM
          FROM
            HBOARD_COMMENT HC JOIN HBOARD H
          ON
            HC.COMMENT_HBOARD = H.IDX
          AND
            HC.COMMENT_HBOARD = IN_IDX
       ) T
       WHERE T.RN   BETWEEN   V_STARTNUM  AND   V_ENDNUM;
    
  END PROC_BOARD_COMMENTPAGINGLIST;

  -- 댓글 조회
  PROCEDURE PROC_BOARD_COMMENTVIEW( IN_COMMENT_IDX IN NUMBER, O_CUR OUT SYS_REFCURSOR) AS
  BEGIN
 
    -- IDX 로 조회
  
    OPEN O_CUR FOR
    SELECT 
        COMMENT_IDX,
        COMMENT_HBOARD,
        COMMENT_WRITER,
        TO_CHAR(COMMENT_REGDATE, 'YYYY-MM-DD HH24:MI:SS') COMMENT_REGDATE,
        COMMENT_CONT,
        COMMENT_BNUM,
        COMMENT_LVL,
        COMMENT_STEP,
        COMMENT_NREF,
        COMMENT_DELNUM
    FROM HBOARD_COMMENT
    WHERE COMMENT_IDX = IN_COMMENT_IDX;
  END PROC_BOARD_COMMENTVIEW;

  -- 게시글 댓글 수정
  PROCEDURE PROC_BOARD_COMMENTUPDATE( IN_COMMENT_IDX IN NUMBER, IN_COMMENT_HBOARD IN NUMBER, IN_COMMENT_CONT IN VARCHAR2) AS
  BEGIN
    UPDATE
    (
    SELECT HC.COMMENT_IDX, HC.COMMENT_CONT, HC.COMMENT_WRITER
    FROM HBOARD H, HBOARD_COMMENT HC
    WHERE H.IDX = HC.COMMENT_HBOARD
    AND H.IDX = IN_COMMENT_HBOARD
    )
    SET COMMENT_CONT = IN_COMMENT_CONT
    WHERE COMMENT_IDX = IN_COMMENT_IDX;
  END PROC_BOARD_COMMENTUPDATE;

  -- 게시판 댓글 삭제
  PROCEDURE PROC_BOARD_COMMENTDELETE( IN_COMMENT_IDX IN NUMBER ) AS
    V_COMMENT_NREF   NUMBER(5);
    V_COMMENT_LVL    NUMBER(5);
    V_COMMENT_STEP   NUMBER(5);
  BEGIN
    SELECT   COMMENT_NREF,   COMMENT_LVL,   COMMENT_STEP
     INTO    V_COMMENT_NREF, V_COMMENT_LVL, V_COMMENT_STEP
     FROM    HBOARD_COMMENT
     WHERE   COMMENT_IDX  =  IN_COMMENT_IDX;

    -- DELNUM  = 1 로 변경 - 모든글의 DELNUM = 1
    UPDATE   HBOARD_COMMENT
     SET     COMMENT_DELNUM  =  1
     WHERE   COMMENT_IDX     =  IN_COMMENT_IDX;

    -- 실제 글 삭제 - 답글이 없는 글
    DELETE   FROM  HBOARD_COMMENT
     WHERE   NOT   EXISTS (
         SELECT  1  FROM HBOARD_COMMENT
          WHERE  COMMENT_NREF =  V_COMMENT_NREF
           AND   COMMENT_LVL  =  V_COMMENT_LVL + 1
           AND   COMMENT_STEP =  V_COMMENT_STEP + 1
     )
     AND  COMMENT_IDX   =  IN_COMMENT_IDX;
   COMMIT; 
  END PROC_BOARD_COMMENTDELETE;

  -- 게시글 댓글 조회
  PROCEDURE PROC_BOARD_BOARDCOMMENTVIEW( IN_IDX IN NUMBER, O_CUR OUT SYS_REFCURSOR) AS
  BEGIN
   OPEN O_CUR FOR
    SELECT 
    COMMENT_IDX, COMMENT_HBOARD, COMMENT_WRITER,
    TO_CHAR(COMMENT_REGDATE, 'YYYY-MM-DD HH24:MI:SS') COMMENT_REGDATE,
    COMMENT_CONT, COMMENT_BNUM, COMMENT_LVL, COMMENT_STEP, COMMENT_NREF, COMMENT_DELNUM
    FROM HBOARD_COMMENT HC JOIN HBOARD H
    ON HC.COMMENT_HBOARD = H.IDX
    AND COMMENT_HBOARD = IN_IDX;
  END PROC_BOARD_BOARDCOMMENTVIEW;

END PKG_HBOARD;